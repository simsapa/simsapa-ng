cmake_minimum_required(VERSION 3.24)

project(simsapadhammareader LANGUAGES CXX)

# Rust always links against non-debug Windows runtime on *-msvc targets
# Note it is best to set this on the command line to ensure all targets are consistent
# https://github.com/corrosion-rs/corrosion/blob/master/doc/src/common_issues.md#linking-debug-cc-libraries-into-rust-fails-on-windows-msvc-targets
# https://github.com/rust-lang/rust/issues/39016
if (CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
  set(CMAKE_MSVC_RUNTIME_LIBRARY "MultiThreadedDLL")
endif()

# Detect and set CMAKE_PREFIX_PATH for Qt6 on macOS
if (APPLE AND NOT IOS)
    # Find the macOS SDK path using xcrun (more reliable than hardcoding)
    execute_process(
        COMMAND xcrun --sdk macosx --show-sdk-path
        OUTPUT_VARIABLE MACOS_SDK_PATH
        OUTPUT_STRIP_TRAILING_WHITESPACE
        ERROR_QUIET
    )

    # Only set CMAKE_OSX_SYSROOT if we found a valid SDK path
    if(MACOS_SDK_PATH AND EXISTS "${MACOS_SDK_PATH}")
        set(CMAKE_OSX_SYSROOT "${MACOS_SDK_PATH}" CACHE PATH "macOS SDK path" FORCE)
        message(STATUS "Using macOS SDK: ${CMAKE_OSX_SYSROOT}")
    else()
        message(WARNING "macOS SDK not found via xcrun, using system default")
        # Unset to use system default
        unset(CMAKE_OSX_SYSROOT CACHE)
    endif()

    # Try to find Qt6 installation if CMAKE_PREFIX_PATH is not already set
    if(NOT CMAKE_PREFIX_PATH)
        if(EXISTS "$ENV{HOME}/Qt/6.8.3/macos")
            set(CMAKE_PREFIX_PATH "$ENV{HOME}/Qt/6.8.3/macos")
        elseif(EXISTS "/opt/Qt/6.8.3/macos")
            set(CMAKE_PREFIX_PATH "/opt/Qt/6.8.3/macos")
        elseif(EXISTS "/usr/local/Qt/6.8.3/macos")
            set(CMAKE_PREFIX_PATH "/usr/local/Qt/6.8.3/macos")
        else()
            message(WARNING "Qt6 installation not found in standard locations. Please set CMAKE_PREFIX_PATH manually.")
        endif()
    endif()
    message(STATUS "Using CMAKE_PREFIX_PATH: ${CMAKE_PREFIX_PATH}")
endif()

set(CMAKE_AUTOMOC ON)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set(CMAKE_AUTORCC_OPTIONS --format-version 1)
list(APPEND CMAKE_AUTORCC_OPTIONS --compress-algo zlib)

set(CXXQT_QTCOMPONENTS Core Gui Qml QuickControls2 QuickTest Test QmlImportScanner)

list(APPEND app_components Widgets Quick)

list(APPEND qt_modules
        Qt::Core
        Qt::Gui
        Qt::Qml
        Qt::Network
        Qt::Widgets
        Qt::Quick
        Qt::QuickControls2)

if (ANDROID)
    set(qmake_path "$ENV{HOME}/Qt/6.8.3/android_x86_64/bin/qmake6")
    list(APPEND app_components WebView)
    list(APPEND qt_modules Qt::WebView)
    add_compile_definitions(Q_OS_ANDROID)
elseif (IOS)
    set(Rust_CARGO_TARGET aarch64-apple-ios)
    set(qmake_path "$ENV{HOME}/Qt/6.8.3/ios/bin/qmake6")
    list(APPEND app_components WebView)
    list(APPEND qt_modules Qt::WebView)
elseif (APPLE)
    # Explicitly set Rust target for macOS to prevent iOS cross-compilation
    if(CMAKE_HOST_SYSTEM_PROCESSOR MATCHES "arm64|aarch64")
        set(Rust_CARGO_TARGET aarch64-apple-darwin)
    else()
        set(Rust_CARGO_TARGET x86_64-apple-darwin)
    endif()

    set(qmake_path "$ENV{HOME}/Qt/6.8.3/macos/bin/qmake6")
    list(APPEND app_components WebEngineQuick)
    list(APPEND qt_modules Qt::WebEngineQuick)
else()
    set(qmake_path "$ENV{HOME}/Qt/6.8.3/gcc_64/bin/qmake6")
    list(APPEND app_components WebEngineQuick)
    list(APPEND qt_modules Qt::WebEngineQuick)
endif()

find_package(Qt6 COMPONENTS ${CXXQT_QTCOMPONENTS} ${app_components})

find_package(CxxQt QUIET)
if(NOT CxxQt_FOUND)
    include(FetchContent)
    FetchContent_Declare(
        CxxQt
        GIT_REPOSITORY https://github.com/kdab/cxx-qt-cmake.git
        GIT_TAG 0.7
    )

    FetchContent_MakeAvailable(CxxQt)
endif()

# CXX-Qt (using Corrosion) creates a CMake target with the same name as the crate.
cxx_qt_import_crate(
    MANIFEST_PATH bridges/Cargo.toml 
    CRATES simsapa_bridges
    LOCKED
    QMAKE ${qmake_path}

    QT_MODULES ${qt_modules}
)

cxx_qt_import_qml_module(simsapa_qml_module
    URI "com.profoundlabs.simsapa"
    SOURCE_CRATE simsapa_bridges)

qt_add_resources(icons "assets/icons.qrc")

list(APPEND cpp_files
    cpp/main.cpp
    cpp/errors.cpp
    cpp/utils.cpp
    cpp/wake_lock.cpp
    cpp/system_palette.cpp
    cpp/clipboard_manager.cpp
    cpp/gui.cpp
    cpp/window_manager.cpp
    cpp/sutta_search_window.cpp
    cpp/download_appdata_window.cpp
    cpp/word_lookup_window.cpp
    cpp/sutta_languages_window.cpp
    cpp/library_window.cpp)

list(APPEND app_files ${cpp_files} ${icons})

if (WIN32)
    set(app_icon_resource_windows "${CMAKE_CURRENT_SOURCE_DIR}/assets/icons/appicons/simsapa.rc")

    qt_add_executable(simsapadhammareader
        ${cpp_files}
        ${icons}
        ${app_icon_resource_windows})

elseif (APPLE)
    # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
    # generated by CMake. This variable contains the .icns file name,
    # without the path.
    set(MACOSX_BUNDLE_ICON_FILE simsapa.icns)

    # And the following tells CMake where to find and install the file itself.
    set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/assets/icons/appicons/simsapa.icns")
    set_source_files_properties(${app_icon_macos} PROPERTIES MACOSX_PACKAGE_LOCATION "Resources")

    qt_add_executable(simsapadhammareader MACOSX_BUNDLE ${app_files} ${app_icon_macos})

elseif (ANDROID)
    list(APPEND android_files
        android/AndroidManifest.xml
        android/build.gradle
        android/gradle.properties
        android/gradlew
        android/gradlew.bat
        android/gradle/wrapper/gradle-wrapper.jar
        android/gradle/wrapper/gradle-wrapper.properties
        android/res/values/libs.xml
        android/res/xml/network_security_config.xml
        android/res/xml/qtprovider_paths.xml)

    qt_add_executable(simsapadhammareader ${app_files} ${android_files})

else()
    qt_add_executable(simsapadhammareader ${app_files})

endif()

set_target_properties(simsapadhammareader PROPERTIES
    WIN32_EXECUTABLE TRUE
    MACOSX_BUNDLE TRUE
    QT_ANDROID_PACKAGE_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/android
)

# Link to the qml module, which in turn links to the Rust simsapa library
target_link_libraries(simsapadhammareader
    PRIVATE
        simsapa_qml_module
    PUBLIC
        simsapa_bridges
        ${qt_modules}
    )

# If we are using a statically linked Qt then we need to import any qml plugins
qt_import_qml_plugins(simsapadhammareader)
